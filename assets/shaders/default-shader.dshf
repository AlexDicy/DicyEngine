#version 410 core

layout (location = 0) out vec4 color;

in vec3 vPosition;
in vec3 vNormal;
in vec3 vNormalWorld;
in vec2 vTextureCoords;
in vec4 vDirectionalLightSpacePosition;

struct Material {
    bool ignoreLighting;
};

struct DirectionalLight {
    vec3 color;
    float intensity;
    vec3 direction;
};

struct PointLight {
    vec3 position;
    vec3 color;
    float intensity;
    float farPlane;
};

uniform vec3 uCameraPosition;
// material
uniform Material uMaterial;
uniform sampler2D uAlbedo;
uniform sampler2D uOcclusionRoughnessMetallic;
// environment
uniform vec3 uIrradianceSH[9];
uniform samplerCube uPrefilteredEnvMap;
uniform sampler2D uBRDFLUT;
// directional light
uniform DirectionalLight uDirectionalLight;
uniform sampler2D uDirectionalShadowMap;
// point lights
uniform PointLight uPointLights[10];
uniform int uPointLightsCount;
uniform samplerCubeArray uPointShadowMaps;

#define PI 3.14159265359
#define PREFILTERED_LEVELS 5

#define saturate(x) clamp(x, 0.0, 1.0)


float getShadow2D(sampler2D shadowMap, vec4 lightSpacePosition);
float getShadowCubeArray(samplerCubeArray shadowMap, PointLight light, float lightIndex);
float distributionGGX(float NoH, float roughness2);
float geometrySchlickGGX(float NoV, float roughness);
vec3 fresnelSchlick(float VoH, vec3 F0);
vec3 fresnelSchlickRoughness(float cosTheta, vec3 F0, float roughness);
vec3 calculatePointLightsWithShadow(vec3 N, vec3 V, float NoV, vec3 albedo, float metallic, float roughness2, vec3 F0);
vec3 calculateAmbientLight(vec3 N, vec3 V, float NoV, vec3 albedo, float metallic, float occlusion, float roughness, vec3 F0);
vec3 calculateLight(float NoV, float NoL, float NoH, float VoH, vec3 albedo, float metallic, float roughness2, vec3 F0, vec3 radiance);
vec3 calculateIrradianceSphericalHarmonics(const vec3 normal);

void main() {
    vec4 linearAlbedo = texture(uAlbedo, vTextureCoords);
    if (uMaterial.ignoreLighting) {
        color = linearAlbedo;
        return;
    }

    vec3 albedo = pow(linearAlbedo.rgb, vec3(2.2));
    vec4 occlusionRoughnessMetallic = texture(uOcclusionRoughnessMetallic, vTextureCoords);
    float occlusion = occlusionRoughnessMetallic.r;
    float roughness = occlusionRoughnessMetallic.g;
    float metallic = occlusionRoughnessMetallic.b;


    // common calculations for all lights
    vec3 N = normalize(vNormal);
    vec3 V = normalize(uCameraPosition - vPosition);
    float NoV = saturate(dot(N, V));
    float roughness2 = clamp(roughness * roughness, 1e-3, 1.0);
    // F0 is calculated using the IOR (index of refraction), we approximate it to 0.04 for non-metals and use the albedo for metals
    vec3 F0 = mix(vec3(0.04), albedo, metallic);

    // directional light
    vec3 L = normalize(-uDirectionalLight.direction);
    vec3 H = normalize(V + L);
    float NoL = saturate(dot(N, L));
    float NoH = saturate(dot(N, H));
    float VoH = saturate(dot(V, H));
    float shadow = 1.0 - getShadow2D(uDirectionalShadowMap, vDirectionalLightSpacePosition);
    vec3 directionalLight = calculateLight(NoV, NoL, NoH, VoH, albedo, metallic, roughness2, F0, uDirectionalLight.color * uDirectionalLight.intensity);
    vec3 totalLight = directionalLight * shadow;

    // point lights
    totalLight += calculatePointLightsWithShadow(N, V, NoV, albedo, metallic, roughness2, F0);

    // ambient light
    vec3 ambient = calculateAmbientLight(N, V, NoV, albedo, metallic, occlusion, roughness, F0);

    vec3 finalColor = ambient + totalLight;

    vec3 linearColor = vec3(finalColor);
    linearColor = linearColor / (linearColor + vec3(1.0));
    color = vec4(pow(linearColor, vec3(1.0 / 2.2)), linearAlbedo.a);
}

//
// Shadows
//
float getShadow2D(sampler2D shadowMap, vec4 lightSpacePosition) {
    const float bias = 0.0005;
    vec3 lightSpaceCoords = (lightSpacePosition.xyz / lightSpacePosition.w) * 0.5 + 0.5;
    float shadowDepth = texture(shadowMap, lightSpaceCoords.xy).r + 0.0005;

    float shadow = 0.0;
    vec2 texelSize = 1.0 / textureSize(shadowMap, 0);
    for (int x = -1; x <= 1; x++) {
        for (int y = -1; y <= 1; y++) {
            float pcfDepth = texture(shadowMap, lightSpaceCoords.xy + vec2(x, y) * texelSize).r + bias;
            shadow += lightSpaceCoords.z > pcfDepth ? 1.0 : 0.0;
        }
    }
    return shadow / 9.0;
}

// @formatter:off
vec3 shadowSampleOffsetDirections[20] = vec3[](
    vec3(1, 1, 1), vec3(1, -1, 1), vec3(-1, -1, 1), vec3(-1, 1, 1),
    vec3(1, 1, -1), vec3(1, -1, -1), vec3(-1, -1, -1), vec3(-1, 1, -1),
    vec3(1, 1, 0), vec3(1, -1, 0), vec3(-1, -1, 0), vec3(-1, 1, 0),
    vec3(1, 0, 1), vec3(-1, 0, 1), vec3(1, 0, -1), vec3(-1, 0, -1),
    vec3(0, 1, 1), vec3(0, -1, 1), vec3(0, -1, -1), vec3(0, 1, -1)
);
// @formatter:on

float getShadowCubeArray(samplerCubeArray shadowMap, PointLight light, float lightIndex) {
    vec3 lightDirection = vPosition - light.position;
    lightDirection.y = -lightDirection.y;
    float depth = length(lightDirection);

    const float bias = 0.01;
    float shadow = 0.0;
    int samples = 20;
    float diskRadius = 0.005;
    for (int i = 0; i < samples; ++i) {
        float sampledDistance = texture(shadowMap, vec4(lightDirection + shadowSampleOffsetDirections[i] * diskRadius, lightIndex)).r;
        if (depth > sampledDistance + bias) {
            shadow += 1.0;
        }
    }
    return shadow / float(samples);
}

//
// Lights
//
vec3 calculatePointLightsWithShadow(vec3 N, vec3 V, float NoV, vec3 albedo, float metallic, float roughness2, vec3 F0) {
    vec3 totalLight = vec3(0.0);
    for (int i = 0; i < uPointLightsCount; i++) {
        vec3 L = normalize(uPointLights[i].position - vPosition);
        vec3 H = normalize(V + L);
        float NoL = saturate(dot(N, L));
        float NoH = saturate(dot(N, H));
        float VoH = saturate(dot(V, H));
        float distance = length(uPointLights[i].position - vPosition);
        float attenuation = 1.0 / (distance * distance);
        vec3 radiance = uPointLights[i].color * uPointLights[i].intensity * attenuation;
        float shadow = 1.0 - getShadowCubeArray(uPointShadowMaps, uPointLights[i], float(i));
        totalLight += calculateLight(NoV, NoL, NoH, VoH, albedo, metallic, roughness2, F0, radiance) * shadow;
    }
    return totalLight;
}

vec3 calculateAmbientLight(vec3 N, vec3 V, float NoV, vec3 albedo, float metallic, float occlusion, float roughness, vec3 F0) {
    vec3 kS = fresnelSchlickRoughness(max(NoV, 0.0), F0, roughness);
    vec3 kD = 1.0 - kS;
    kD *= 1.0 - metallic;

    vec3 indirectDiffuse = calculateIrradianceSphericalHarmonics(N) * albedo;
    vec3 R = reflect(-V, N);
    vec3 prefilteredColor = textureLod(uPrefilteredEnvMap, R, roughness * (PREFILTERED_LEVELS - 1)).rgb;
    vec2 brdf = texture(uBRDFLUT, vec2(NoV, roughness)).rg;
    vec3 specularIBL = prefilteredColor * (kS * brdf.x + brdf.y);
    return (kD * indirectDiffuse + specularIBL) * occlusion;
}

vec3 calculateLight(float NoV, float NoL, float NoH, float VoH, vec3 albedo, float metallic, float roughness2, vec3 F0, vec3 radiance) {
    vec3 F = fresnelSchlick(VoH, F0);
    float D = distributionGGX(NoH, roughness2);
    float G = geometrySchlickGGX(NoV, roughness2) * geometrySchlickGGX(NoL, roughness2);

    vec3 kD = (1.0 - F) * (1.0 - metallic);
    vec3 diffuse = kD * albedo / PI; // lambert brdf
    vec3 specular = F * G * D / max(4.0 * NoV * NoL, 0.0001); // cook-torrance brdf
    return (diffuse + specular) * radiance * NoL;
}

vec3 calculateIrradianceSphericalHarmonics(const vec3 normal) {
    // band 0
    vec3 sh = uIrradianceSH[0]
    // band 1
    + uIrradianceSH[1] * (normal.y)
    + uIrradianceSH[2] * (normal.z)
    + uIrradianceSH[3] * (normal.x)
    // band 2
    + uIrradianceSH[4] * (normal.y * normal.x)
    + uIrradianceSH[5] * (normal.y * normal.z)
    + uIrradianceSH[6] * (3.0 * normal.z * normal.z - 1.0)
    + uIrradianceSH[7] * (normal.z * normal.x)
    + uIrradianceSH[8] * (normal.x * normal.x - normal.y * normal.y);
    return max(sh, 0.0);
}

// Trowbridge–Reitz (GGX) distribution
float distributionGGX(float NoH, float roughness2) {
    float oneMinusNoH2 = 1.0 - NoH * NoH;
    float a = NoH * roughness2;
    float k = roughness2 / (oneMinusNoH2 + a * a);
    return k * k * (1.0 / PI);
}

// Schlick geometric shadowing/masking approximation
float geometrySchlickGGX(float NoV, float roughness) {
    float a = roughness * roughness;
    float k = a / 2.0;
    return NoV / (NoV * (1.0 - k) + k);
}

// Fresnel-Schlick approximation
vec3 fresnelSchlick(float VoH, vec3 F0) {
    return F0 + (1.0 - F0) * pow(1.0 - VoH, 5);
}

// Fresnel-Schlick approximation with roughness, Sébastien Lagarde implementation
vec3 fresnelSchlickRoughness(float cosTheta, vec3 F0, float roughness) {
    return F0 + (max(vec3(1.0 - roughness), F0) - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

